plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.graphics']
}

application {
    mainClass = 'hangman.console.ConsoleApp'
    mainModule = 'hangman'
}

run {
    standardInput = System.in
}

task GUI(type: JavaExec) {
    group = 'application'
    description = 'Runs the Hangman GUI application.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hangman.gui.HangmanGUI'
    mainModule = 'hangman'
}

task console(type: JavaExec) {
    group = 'application'
    description = 'Runs the Hangman Console application.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hangman.console.ConsoleApp'
    mainModule = 'hangman'
    standardInput = System.in
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen { false }
}
